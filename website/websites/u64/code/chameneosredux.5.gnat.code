<span class="slc">-- The Computer Language Benchmarks Game</span>
<span class="slc">-- http://benchmarksgame.alioth.debian.org/</span>
<span class="slc">--</span>
<span class="slc">-- Contributed by Francois Fabien (9 septembre 2011)</span>
<span class="slc">--</span>
<span class="slc">-- Based on the original Ada version by</span>
<span class="slc">-- Claude Kaiser and Jean-Francois Pradat-Peyre (CEDRIC - CNAM Paris)</span>
<span class="slc">--</span>
<span class="slc">-- Chameneos are Ada tasks, mapped to OS threads.</span>
<span class="slc">-- The meeting place is a protected body with a requeue entry.</span>
<span class="slc">-- On a single-core, tests are run in sequence; on a multicore in parallel.</span>
<span class="slc">--</span>
<span class="slc">-- Expected build command:</span>
<span class="slc">-- gnatmake -O3  chameneosredux.adb</span>
<span class="slc">------------------------------------------------------------------------------</span>
<span class="kwa">pragma</span> Suppress (All_Checks);

<span class="kwa">pragma</span> Restrictions (No_Abort_Statements);
<span class="kwa">pragma</span> Restrictions (Max_Select_Alternatives =&gt; <span class="num">0</span>);
<span class="kwa">pragma</span> Restrictions (Max_Asynchronous_Select_Nesting =&gt; <span class="num">0</span>);

<span class="kwa">with</span> System;           <span class="kwa">use</span> System;
<span class="kwa">with</span> Ada.Command_Line; <span class="kwa">use</span> Ada.Command_Line;
<span class="kwa">with</span> Chameneos;        <span class="kwa">use</span> Chameneos;
<span class="kwa">with</span> System.Task_Info;

<span class="kwa">procedure</span> ChameneosRedux <span class="kwa">is</span>
   <span class="kwa">pragma</span> Priority (Max_Priority);

   <span class="slc">-- GNU ld will remove unused sections; GNAT specific</span>
   <span class="kwa">pragma</span> Linker_Options (<span class="str">&quot;-Wl,--gc-sections&quot;</span>);

   Meeting_count : Meetings := <span class="num">600</span>; <span class="slc">-- default value</span>
<span class="kwa">begin</span>
   <span class="kwa">if</span> Argument_Count &gt; <span class="num">0</span> <span class="kwa">then</span>
      <span class="kwa">begin</span>
         Meeting_count := Meetings<span class="kwd">'Value</span> (Argument (<span class="num">1</span>));
      <span class="kwa">exception</span>
         <span class="kwa">when others</span> =&gt;
            <span class="slc">-- When wrong argument, keep meetings := 600</span>
            <span class="kwa">null</span>;
      <span class="kwa">end</span>;
   <span class="kwa">end if</span>;

   Chameneos.Print_Colours;
   <span class="kwa">if</span> Task_Info.Number_Of_Processors &gt; <span class="num">1</span> <span class="kwa">then</span>
      Chameneos.Run_Multicore (Meeting_count);
   <span class="kwa">else</span>
      Chameneos.Run (Meeting_count, <span class="num">3</span>);
      Chameneos.Run (Meeting_count, <span class="num">10</span>);
   <span class="kwa">end if</span>;
<span class="kwa">end</span> ChameneosRedux;
<span class="slc">------------------------------------------------------------------------------</span>
<span class="slc">-- Root package</span>
<span class="slc">-- Definitions and test procedure (run)</span>
<span class="slc">------------------------------------------------------------------------------</span>

<span class="kwa">package</span> Chameneos <span class="kwa">is</span>

   Max_Creatures : <span class="kwa">constant</span> := <span class="num">10</span>;

   <span class="kwa">type</span> Meetings <span class="kwa">is range</span> <span class="num">0</span> .. +(<span class="num">2</span> ** <span class="num">31</span> - <span class="num">1</span>);
   <span class="kwa">for</span> Meetings<span class="kwd">'Size</span> <span class="kwa">use</span> <span class="num">32</span>; <span class="slc">--32bits even on x64</span>

   <span class="kwa">type</span> Colour <span class="kwa">is</span> (Blue, Red, Yellow);

   <span class="slc">-- The creature Name</span>
   <span class="kwa">type</span> Id_Type <span class="kwa">is range</span> <span class="num">1</span> .. Max_Creatures;

   Max_Set : <span class="kwa">constant array</span> (Id_Type) <span class="kwa">of</span> Colour :=
     (Blue,
      Red,
      Yellow,
      Red,
      Yellow,
      Blue,
      Red,
      Yellow,
      Red,
      Blue);

   <span class="slc">-- The test procedure for single core</span>
   <span class="kwa">procedure</span> Run (Meetings_Nbr : Meetings; Task_Nbr : Id_Type);
   <span class="slc">-- The test procedure for multicore</span>
   <span class="kwa">procedure</span> Run_Multicore (Meetings_Nbr : Meetings);

   <span class="slc">--  Ancilliary procedure used for output</span>
   <span class="kwa">procedure</span> Print_Colours;

   <span class="kwa">function</span> Do_Complement (C1, C2 : Colour) <span class="kwa">return</span> Colour;
   <span class="slc">-- since this function is heavily used, make it inline to speed up</span>
   <span class="kwa">pragma</span> Inline (Do_Complement);

<span class="kwa">end</span> Chameneos;
<span class="slc">------------------------------------------------------------------------------</span>
<span class="kwa">with</span> Ada.Text_IO;           <span class="kwa">use</span> Ada.Text_IO;
<span class="kwa">with</span> Ada.Strings.Unbounded; <span class="kwa">use</span> Ada.Strings.Unbounded;
<span class="kwa">with</span> Chameneos.Creatures;   <span class="kwa">use</span> Chameneos.Creatures;

<span class="kwa">package body</span> Chameneos <span class="kwa">is</span>

   <span class="kwa">package</span> Meet_Io <span class="kwa">is new</span> Ada.Text_IO.Integer_IO (Meetings);

   <span class="kwa">type</span> Measures <span class="kwa">is array</span> (Id_Type <span class="kwa">range</span> &lt;&gt;) <span class="kwa">of</span> Measure_Item;

   <span class="kwa">type</span> String_Access <span class="kwa">is access all</span> <span class="kwb">String</span>;

   Colour_Image : <span class="kwa">constant array</span> (Colour) <span class="kwa">of</span> String_Access :=
     (Blue   =&gt; <span class="kwa">new</span> <span class="kwb">String</span><span class="str">'(&quot;blue&quot;),</span>
<span class="str">      Red    =&gt; new String'</span>(<span class="str">&quot;red&quot;</span>),
      Yellow =&gt; <span class="kwa">new</span> <span class="kwb">String</span><span class="str">'(&quot;yellow&quot;));</span>
<span class="str"></span>
<span class="str">   -- global data because of memory leak</span>
<span class="str">   Numbers_Image : constant array (0 .. 9) of String_Access :=</span>
<span class="str">     (0 =&gt; new String'</span>(<span class="str">&quot;zero &quot;</span>),
      <span class="num">1</span> =&gt; <span class="kwa">new</span> <span class="kwb">String</span><span class="str">'(&quot;one &quot;),</span>
<span class="str">      2 =&gt; new String'</span>(<span class="str">&quot;two &quot;</span>),
      <span class="num">3</span> =&gt; <span class="kwa">new</span> <span class="kwb">String</span><span class="str">'(&quot;three &quot;),</span>
<span class="str">      4 =&gt; new String'</span>(<span class="str">&quot;four &quot;</span>),
      <span class="num">5</span> =&gt; <span class="kwa">new</span> <span class="kwb">String</span><span class="str">'(&quot;five &quot;),</span>
<span class="str">      6 =&gt; new String'</span>(<span class="str">&quot;six &quot;</span>),
      <span class="num">7</span> =&gt; <span class="kwa">new</span> <span class="kwb">String</span><span class="str">'(&quot;seven &quot;),</span>
<span class="str">      8 =&gt; new String'</span>(<span class="str">&quot;eight &quot;</span>),
      <span class="num">9</span> =&gt; <span class="kwa">new</span> <span class="kwb">String</span><span class="str">'(&quot;nine &quot;));</span>
<span class="str"></span>
<span class="str">   function Do_Complement (C1, C2 : Colour) return Colour is</span>
<span class="str">   begin</span>
<span class="str">      case C1 is</span>
<span class="str">         when Blue =&gt;</span>
<span class="str">            case C2 is</span>
<span class="str">               when Blue =&gt;</span>
<span class="str">                  return Blue;</span>
<span class="str">               when Red =&gt;</span>
<span class="str">                  return Yellow;</span>
<span class="str">               when Yellow =&gt;</span>
<span class="str">                  return Red;</span>
<span class="str">            end case;</span>
<span class="str">         when Red =&gt;</span>
<span class="str">            case C2 is</span>
<span class="str">               when Blue =&gt;</span>
<span class="str">                  return Yellow;</span>
<span class="str">               when Red =&gt;</span>
<span class="str">                  return Red;</span>
<span class="str">               when Yellow =&gt;</span>
<span class="str">                  return Blue;</span>
<span class="str">            end case;</span>
<span class="str">         when Yellow =&gt;</span>
<span class="str">            case C2 is</span>
<span class="str">               when Blue =&gt;</span>
<span class="str">                  return Red;</span>
<span class="str">               when Red =&gt;</span>
<span class="str">                  return Blue;</span>
<span class="str">               when Yellow =&gt;</span>
<span class="str">                  return Yellow;</span>
<span class="str">            end case;</span>
<span class="str">      end case;</span>
<span class="str">   end Do_Complement;</span>
<span class="str"></span>
<span class="str">   function Spelled (Value : Meetings) return String is</span>
<span class="str">      Result : Unbounded_String := Null_Unbounded_String;</span>
<span class="str">      K      : Natural          := Natural (Value);</span>
<span class="str">   begin</span>
<span class="str">      loop</span>
<span class="str">         Result := Numbers_Image (K rem 10).all &amp; Result;</span>
<span class="str">         K      := K / 10;</span>
<span class="str">         exit when K = 0;</span>
<span class="str">      end loop;</span>
<span class="str">      return To_String (Result);</span>
<span class="str">   end Spelled;</span>
<span class="str"></span>
<span class="str">   procedure Join (Set : Creature_Sets) is</span>
<span class="str">      Finished : Boolean := False;</span>
<span class="str">      First    : Id_Type := Id_Type'First;</span>
<span class="str">   begin</span>
<span class="str">      while not Finished loop</span>
<span class="str">         Finished := True;</span>
<span class="str">         for I in First .. Set'Last loop</span>
<span class="str">            if not Set (I)'Terminated then</span>
<span class="str">               Finished := False;</span>
<span class="str">               delay 0.001;</span>
<span class="str">               exit;</span>
<span class="str">            else</span>
<span class="str">               First := I;</span>
<span class="str">            end if;</span>
<span class="str">         end loop;</span>
<span class="str">      end loop;</span>
<span class="str">   end Join;</span>
<span class="str">   pragma Inline (Join);</span>
<span class="str"></span>
<span class="str">   procedure Output (Measure : in Measures) is</span>
<span class="str">      Total_Meetings : Meetings := 0;</span>
<span class="str">   begin</span>
<span class="str">      for I in Measure'Range loop</span>
<span class="str">         Put (Colour_Image (Max_Set (I)).all &amp; &quot; &quot;);</span>
<span class="str">      end loop;</span>
<span class="str">      New_Line;</span>
<span class="str">      for I in Measure'Range loop</span>
<span class="str">         Total_Meetings := Total_Meetings + Measure (I).Meetings_Made;</span>
<span class="str">         Meet_Io.Put (Measure (I).Meetings_Made, Width =&gt; 10);</span>
<span class="str">         Put_Line (&quot;  &quot; &amp; Spelled (Measure (I).Meetings_Same));</span>
<span class="str">      end loop;</span>
<span class="str"></span>
<span class="str">      Put (Spelled (Total_Meetings));</span>
<span class="str">      New_Line (2);</span>
<span class="str">   end Output;</span>
<span class="str"></span>
<span class="str">   procedure Run (Meetings_Nbr : Meetings; Task_Nbr : Id_Type) is</span>
<span class="str"></span>
<span class="str">      Creature_Set : Creature_Sets (1 .. Task_Nbr);</span>
<span class="str">      Measure      : Measures (1 .. Task_Nbr);</span>
<span class="str">   begin</span>
<span class="str">      -- Meeting Place initialization</span>
<span class="str">      Init_Mall (1, Meetings_Nbr);</span>
<span class="str">      -- launch the tasks</span>
<span class="str">      for I in 1 .. Task_Nbr loop</span>
<span class="str">         Creature_Set (I).Start (1, I, Max_Set (I));</span>
<span class="str">      end loop;</span>
<span class="str"></span>
<span class="str">      -- Rendez-vous : Waiting for the the completion of tasks</span>
<span class="str">      for I in 1 .. Task_Nbr loop</span>
<span class="str">         Creature_Set (I).Report (Measure (I));</span>
<span class="str">      end loop;</span>
<span class="str">      Join (Creature_Set);</span>
<span class="str">      Output (Measure);</span>
<span class="str"></span>
<span class="str">   end Run;</span>
<span class="str"></span>
<span class="str">   procedure Run_Multicore (Meetings_Nbr : Meetings) is</span>
<span class="str"></span>
<span class="str">      Creature_Set_3  : Creature_Sets (1 .. 3);</span>
<span class="str">      Measure_3       : Measures (1 .. 3);</span>
<span class="str">      Creature_Set_10 : Creature_Sets (1 .. 10);</span>
<span class="str">      Measure_10      : Measures (1 .. 10);</span>
<span class="str"></span>
<span class="str">   begin</span>
<span class="str">      -- initialization of the 2 Meeting Places</span>
<span class="str">      Init_Mall (1, Meetings_Nbr);</span>
<span class="str">      Init_Mall (2, Meetings_Nbr);</span>
<span class="str">      -- launch the tasks</span>
<span class="str">      for I in Id_Type range 1 .. 3 loop</span>
<span class="str">         Creature_Set_3 (I).Start (1, I, Max_Set (I));</span>
<span class="str">      end loop;</span>
<span class="str">      for I in Id_Type range 1 .. 10 loop</span>
<span class="str">         Creature_Set_10 (I).Start (2, I, Max_Set (I));</span>
<span class="str">      end loop;</span>
<span class="str"></span>
<span class="str">      -- Rendez-vous : Waiting for the the completion of tasks</span>
<span class="str">      for I in Id_Type range 1 .. 10 loop</span>
<span class="str">         Creature_Set_10 (I).Report (Measure_10 (I));</span>
<span class="str">      end loop;</span>
<span class="str">      Join (Creature_Set_10);</span>
<span class="str">      for I in Id_Type range 1 .. 3 loop</span>
<span class="str">         Creature_Set_3 (I).Report (Measure_3 (I));</span>
<span class="str">      end loop;</span>
<span class="str">      Join (Creature_Set_3);</span>
<span class="str"></span>
<span class="str">      Output (Measure_3);</span>
<span class="str">      Output (Measure_10);</span>
<span class="str"></span>
<span class="str">   end Run_Multicore;</span>
<span class="str"></span>
<span class="str">   procedure Print_Colours is</span>
<span class="str">      procedure Print_One_Line (C1, C2 : Colour) is</span>
<span class="str">      begin</span>
<span class="str">         Put_Line</span>
<span class="str">           (Colour_Image (C1).all &amp;</span>
<span class="str">            &quot; + &quot; &amp;</span>
<span class="str">            Colour_Image (C2).all &amp;</span>
<span class="str">            &quot; -&gt; &quot; &amp;</span>
<span class="str">            Colour_Image (Do_Complement (C1, C2)).all);</span>
<span class="str">      end Print_One_Line;</span>
<span class="str">   begin</span>
<span class="str">      for Self in Colour loop</span>
<span class="str">         for Other in Colour loop</span>
<span class="str">            Print_One_Line (Self, Other);</span>
<span class="str">         end loop;</span>
<span class="str">      end loop;</span>
<span class="str">      New_Line;</span>
<span class="str">   end Print_Colours;</span>
<span class="str"></span>
<span class="str">end Chameneos;</span>
<span class="str">------------------------------------------------------------------------------</span>
<span class="str">-- The chameneos description</span>
<span class="str">------------------------------------------------------------------------------</span>
<span class="str">with System;</span>
<span class="str"></span>
<span class="str">package Chameneos.Creatures is</span>
<span class="str"></span>
<span class="str">   type Place_Index is range 1 .. 2;</span>
<span class="str"></span>
<span class="str">   -- Stuff required for the measurement</span>
<span class="str">   type Measure_Item is record</span>
<span class="str">      Meetings_Made : Meetings := 0;</span>
<span class="str">      Meetings_Same : Meetings := 0;</span>
<span class="str">   end record;</span>
<span class="str"></span>
<span class="str">   --A chameneos is an Ada task</span>
<span class="str">   task type Creature is</span>
<span class="str">      pragma Priority (System.Default_Priority);</span>
<span class="str">      pragma Storage_Size (31000);</span>
<span class="str">      entry Start</span>
<span class="str">        (Place_Nbr : in Place_Index;</span>
<span class="str">         Id        : in Id_Type;</span>
<span class="str">         C         : in Colour);</span>
<span class="str">      entry Report (Item : out Measure_Item);</span>
<span class="str">   end Creature;</span>
<span class="str"></span>
<span class="str">   type Creature_Sets is array (Id_Type range &lt;&gt;) of Creature;</span>
<span class="str"></span>
<span class="str">   procedure Init_Mall (Place_Nbr : Place_Index; Max_Count : in Meetings);</span>
<span class="str"></span>
<span class="str">end Chameneos.Creatures;</span>
<span class="str">------------------------------------------------------------------------------</span>
<span class="str"></span>
<span class="str">package body Chameneos.Creatures is</span>
<span class="str"></span>
<span class="str">   -- specification of the meeting place where 2 tasks transfer Id.</span>
<span class="str">   -- the entry point is Meet for all tasks, the first incoming task is</span>
<span class="str">   -- requeued at the Waiting entry.</span>
<span class="str"></span>
<span class="str">   protected type Places is</span>
<span class="str">      procedure Init (Max_Count : in Meetings);</span>
<span class="str">      entry Meet</span>
<span class="str">        (X           : in Id_Type;</span>
<span class="str">         C           : in Colour;</span>
<span class="str">         Mall_Open   : out Boolean;</span>
<span class="str">         Name_Other  : out Id_Type;</span>
<span class="str">         Color_Other : out Colour);</span>
<span class="str"></span>
<span class="str">   private</span>
<span class="str">      entry Waiting</span>
<span class="str">        (X           : in Id_Type;</span>
<span class="str">         C           : in Colour;</span>
<span class="str">         Mall_Open   : out Boolean;</span>
<span class="str">         Name_Other  : out Id_Type;</span>
<span class="str">         Color_Other : out Colour);</span>
<span class="str"></span>
<span class="str">      First_Call, Ready  : Boolean;</span>
<span class="str">      A_Colour, B_Colour : Colour;</span>
<span class="str">      A_Name, B_Name     : Id_Type;</span>
<span class="str">      Meetings_Counter   : Meetings;</span>
<span class="str">   end Places;</span>
<span class="str"></span>
<span class="str">   Place : array (Place_Index) of Places;</span>
<span class="str"></span>
<span class="str">   task body Creature is</span>
<span class="str">      My_Place                : Place_Index;</span>
<span class="str">      My_ID, Other_Id         : Id_Type;</span>
<span class="str">      My_Colour, Other_Colour : Colour;</span>
<span class="str">      L_Measure               : Measure_Item := (0, 0);</span>
<span class="str">      Is_Mall_Open            : Boolean;</span>
<span class="str">   begin</span>
<span class="str">      accept Start (</span>
<span class="str">        Place_Nbr  : in Place_Index;</span>
<span class="str">         Id        : in Id_Type;</span>
<span class="str">         C         : in Colour) do</span>
<span class="str">         My_Place  := Place_Nbr;</span>
<span class="str">         My_ID     := Id;</span>
<span class="str">         My_Colour := C;</span>
<span class="str">      end Start;</span>
<span class="str"></span>
<span class="str">      loop</span>
<span class="str"></span>
<span class="str">         Place (My_Place).Meet</span>
<span class="str">           (My_ID,</span>
<span class="str">            My_Colour,</span>
<span class="str">            Is_Mall_Open,</span>
<span class="str">            Other_Id,</span>
<span class="str">            Other_Colour);</span>
<span class="str">         if not Is_Mall_Open then</span>
<span class="str">            -- the test is finished</span>
<span class="str">            exit;</span>
<span class="str">         end if;</span>
<span class="str">         My_Colour               := Do_Complement (My_Colour, Other_Colour);</span>
<span class="str">         L_Measure.Meetings_Made := L_Measure.Meetings_Made + 1;</span>
<span class="str">         if Other_Id = My_ID then</span>
<span class="str">            L_Measure.Meetings_Same := L_Measure.Meetings_Same + 1;</span>
<span class="str">         end if;</span>
<span class="str"></span>
<span class="str">      end loop;</span>
<span class="str"></span>
<span class="str">      -- Give the results and die.</span>
<span class="str">      accept Report (Item : out Measure_Item) do</span>
<span class="str">         Item := L_Measure;</span>
<span class="str">      end Report;</span>
<span class="str">   end Creature;</span>
<span class="str"></span>
<span class="str">   protected body Places is</span>
<span class="str"></span>
<span class="str">      procedure Init (Max_Count : in Meetings) is</span>
<span class="str">      begin</span>
<span class="str">         Meetings_Counter := Max_Count;</span>
<span class="str">         First_Call       := True;</span>
<span class="str">         Ready            := True;</span>
<span class="str">      end Init;</span>
<span class="str"></span>
<span class="str">      entry Meet</span>
<span class="str">        (X           : in Id_Type;</span>
<span class="str">         C           : in Colour;</span>
<span class="str">         Mall_Open   : out Boolean;</span>
<span class="str">         Name_Other  : out Id_Type;</span>
<span class="str">         Color_Other : out Colour) when Ready</span>
<span class="str">      is</span>
<span class="str">      begin</span>
<span class="str">         if (Meetings_Counter = 0) then</span>
<span class="str">            -- The test is finished</span>
<span class="str">            Mall_Open := False;</span>
<span class="str">            -- must give dummy values that will not be used anyway</span>
<span class="str">            Name_Other  := 1;</span>
<span class="str">            Color_Other := Red;</span>
<span class="str">            return;</span>
<span class="str">         end if;</span>
<span class="str">         Mall_Open := True;</span>
<span class="str">         if (First_Call) then</span>
<span class="str">            First_Call := False;</span>
<span class="str">            A_Name     := X;</span>
<span class="str">            A_Colour   := C;</span>
<span class="str">            requeue Waiting;</span>
<span class="str">         else</span>
<span class="str">            Meetings_Counter := Meetings_Counter - 1;</span>
<span class="str">            B_Name           := X;</span>
<span class="str">            B_Colour         := C;</span>
<span class="str">            Name_Other       := A_Name;</span>
<span class="str">            Color_Other      := A_Colour;</span>
<span class="str">            Ready            := False; -- block next incoming task</span>
<span class="str">            First_Call       := True;  -- enable Waiting entry</span>
<span class="str">         end if;</span>
<span class="str">      end Meet;</span>
<span class="str"></span>
<span class="str">      entry Waiting</span>
<span class="str">        (X           : in Id_Type;</span>
<span class="str">         C           : in Colour;</span>
<span class="str">         Mall_Open   : out Boolean;</span>
<span class="str">         Name_Other  : out Id_Type;</span>
<span class="str">         Color_Other : out Colour) when First_Call</span>
<span class="str">      is</span>
<span class="str">         pragma Unreferenced (X, C);</span>
<span class="str">      begin</span>
<span class="str">         Mall_Open   := True;</span>
<span class="str">         Name_Other  := B_Name;</span>
<span class="str">         Color_Other := B_Colour;</span>
<span class="str">         Ready       := True; --enable Meet entry</span>
<span class="str">      end Waiting;</span>
<span class="str">   end Places;</span>
<span class="str"></span>
<span class="str">   procedure Init_Mall (Place_Nbr : Place_Index; Max_Count : in Meetings) is</span>
<span class="str">   begin</span>
<span class="str">      Place (Place_Nbr).Init (Max_Count);</span>
<span class="str">   end Init_Mall;</span>
<span class="str"></span>
<span class="str">end Chameneos.Creatures;</span>
